plugins {
    id 'crafting-dead'
}

repositories {
    mavenCentral()
    google()
    maven {
        url 'https://maven.tarbits.co.uk/'
    }
}

mixin {
    add sourceSets.main, 'craftingdeadimmerse.refmap.json'
}

minecraft {
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            taskName 'runImmerseClient'

            workingDirectory project.file('run/client')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            args '--mixin', 'craftingdeadimmerse.mixins.json', '--mixin', 'craftingdead.mixins.json', '--mixin', 'rocketconnector.mixins.json'

            mods {
                craftingdeadimmerse {
                    source sourceSets.main
                }
                craftingdead {
                    source project(':crafting-dead-core').sourceSets.main
                }
            }
        }

        server {
            taskName 'runImmerseServer'

            workingDirectory project.file('run/server')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            args '--mixin', 'craftingdeadimmerse.mixins.json', '--mixin', 'craftingdead.mixins.json', '--mixin', 'rocketconnector.mixins.json'

            mods {
                craftingdeadimmerse {
                    source sourceSets.main
                }
                craftingdead {
                    source project(':crafting-dead-core').sourceSets.main
                }
            }
        }
    }
}

def lwjgl_natives = ['natives-windows', 'natives-linux', 'natives-macos']

configurations {
    lwjgl_native_library
    library.extendsFrom lwjgl_native_library
}

dependencies {
    implementation project(':crafting-dead-core')
    shaded_library (group: 'org.lwjgl', name: 'lwjgl-yoga', version: "${lwjgl_version}") {
        exclude group: 'org.lwjgl', module: 'lwjgl'
    }
    lwjgl_natives.each {
        lwjgl_native_library ("org.lwjgl:lwjgl-yoga:${lwjgl_version}:${it}") {
            exclude group: 'org.lwjgl', module: 'lwjgl'
        }
    }

	shaded_library group: 'net.java.timingframework', name: 'timingframework-core', version: '7.3.1'
	shaded_library ('io.sentry:sentry:5.6.0') {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    
    library group: 'net.rocketpowered', name: 'rocket-api', version: '0.0.5'
    
    runtimeOnly fg.deobf("net.rocketpowered:rocket-connector:${minecraft_version}-0.0.7")
}

shadowJar {
    into 'lwjgl-natives', {
        exclude 'META-INF/'
        from {   
            project.configurations.lwjgl_native_library.collect { it.isDirectory() ? it : zipTree(it) }
        }
    }
}

jar {
    manifest {
        attributes([
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            'MixinConfigs': 'craftingdeadimmerse.mixins.json'
        ])
    }
}
